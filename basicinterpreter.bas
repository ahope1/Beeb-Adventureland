1 REM COPYRIGHT SCOTT ADAMS. 1978. 1-305-862-6917.

REM 5 CLEAR5400:DEFINTA-Z:IFPEEK(16396)<>201THEN CMD"T"
REM X=Y=Z:K=R=V:N=LL=F:TP$=K$:

REM L% = screen width in chars
20 MODE7:VDU23,0,10,8;0;0;0;23,0,11,12;0;0;0;:PRINT:DIM S% 255:L%=40:*FX11,0

78 K=0:D=-1:GOTO190

80 CLS:PRINT'"*** WELCOME TO ADVENTURE LAND.(#4.2) ***":PRINT:PRINT" UNLESS TOLD DIFFERENTLY YOU MUST FIND"'"*TREASURES* AND-RETURN-THEM-TO-THEIR-"'"PROPER-PLACE!"
85 PRINT:PRINT"I'M YOUR PUPPET. GIVE ME ENGLISH"'"COMMANDS THAT ";
90 PRINT"CONSIST OF A NOUN AND"'"VERB. SOME EXAMPLES...":PRINT:PRINT"TO FIND OUT WHAT YOU'RE CARRYING YOU"'"MIGHT SAY: TAKE INVENTORY"'"TO GO INTO A HOLE YOU MIGHT SAY: GO HOLE";"TO SAVE CURRENT GAME: SAVE GAME"
100 PRINT:PRINT"YOU WILL AT TIMES NEED SPECIAL ITEMS TO DO THINGS, BUT I'M SURE YOU'LL BE A GOOD";"ADVENTURER AND FIGURE THESE THINGS OUT."
120 PRINT:INPUT"HAPPY ADVENTURING... HIT ENTER TO START"K$:PROCcls:RETURN

190 GOSUB5000

192 R=AR:LX=LT:DF=0:SF=0:INPUT"RETRIEVE OLD 'SAVED' GAME? "K$:IFLEFT$(K$,1)<>"Y"THEN200
193 INPUT"READY SAVED TAPE "K$:PRINT;INT(IL*5/60)+1;" MINUTES"
195 d=OPENIN("SAV"):INPUT#d,SF,LX,DF,R:FORX=0TOIL:INPUT#d,IA(X):NEXT:CLOSE#d

200 GOSUB80:PROCcls:GOSUB1500:GOTO225

210 PROCb:INPUT"TELL ME WHAT TO DO? "TP$:PRINT:GOSUB1000:IF F PRINT"YOU USE WORD(S) I DON'T KNOW":GOTO210
215 IF IL<=8 ELSE IF IA(9)<>-1 ELSE LX=LX-1:IF LX<0 THEN PRINT"LIGHT HAS RUN OUT":IA(9)=0 ELSE IF LX<25 PRINT"LIGHT RUNS OUT IN ";LX;" TURNS!"
220 GOSUB2000
225 NV(0)=0:GOSUB2000:GOTO210

REM Parse input 
1000 K=0:NT$(0)="":NT$(1)=""
1010 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" " THEN K=1 ELSE NT$(K)=NT$(K)+K$
1030 NEXTX:FORX=0TO1:NV(X)=0:IF NT$(X)="" THEN 1070 ELSE NT$(X)=LEFT$(NT$(X),ln):FOR Y=0TONL:K$=NV$(Y,X):IF LEFT$(K$,1)="*" THEN K$=MID$(K$,2)
1035 IF X=1 IF Y<7 THEN K$=LEFT$(K$,ln)
1040 IF NT$(X)=K$ THEN NV(X)=Y:y=Y:Y=NL:NEXTY:Y=y ELSE NEXTY:GOTO1070
1060 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO1060
1070 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN

1500 IF DF IF IA(9)<>-1 AND IA(9)<>R PRINT"I CAN'T SEE, ITS TOO DARK.":RETURN
1505 K=-1:IFLEFT$(RS$(R),1)="*"PROCp(MID$(RS$(R),2)+"."):GOTO1520
1510 PROCp("I'M IN A "+RS$(R)+".")
1520 FORZ=0TOIL:IF K IF IA(Z)=R PRINT'"VISIBLE ITEMS HERE ARE:":K=0
1524 GOTO1530

1525 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/" ELSE FOR W=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"TP$=LEFT$(TP$,W-1):w=W:W=1:NEXTW:W=w ELSE NEXT W
1526 RETURN

REM 1530 IF IA(Z)<>R THEN 1540 ELSE GOSUB1525:IF POS+LEN(TP$)+3>63 THEN PRINT
1530 IF IA(Z)<>R THEN 1540 ELSE GOSUB1525
1535 PROCp(CHR$(32*-(POS>0))+TP$+"."):PROCj
1540 NEXT:IFPOS PRINT' ELSEPRINT
1550 K=-1:FORZ=0TO5:IF K IF RM(R,Z)<>0 PRINT"OBVIOUS EXITS:";:K=0
1560 IF RM(R,Z)<>0 PROCp(CHR$(32*-(POS>0))+NV$(Z+1,1)):PROCj
1570 NEXT:IF NOT K PRINT
1580 PRINT:RETURN


REM *** Search for matching actions ***

REM Find a matching action
2000 F2=-1:F=-1:F3=0:IF NV(0)=1 AND NV(1)<7 THEN2210 ELSE FORX=0TOCL:V=INT(CA(X,0)/150):IF NV(0)=0 IFV<>0 THEN x=X:X=CL:NEXTX:X=x:GOTO 2930
2005 IF NV(0)<>V THEN NEXTX:GOTO2901 ELSE N=CA(X,0)-V*150
2010 IF NV(0)<>0 ELSE F=0:IF RND(100)<=N THEN 2030 ELSE NEXT X:GOTO2901
2020 IF N<>NV(1) AND N<>0 THEN NEXTX:GOTO2901

REM Process the conditions                        K=condition code, LL="number"
2030 F2=-1:F=0:F3=-1:FORY=1TO5:LL=INT(CA(X,Y)/20):K=CA(X,Y)-LL*20:F1=-1:ON K+1 GOTO 2140,2050,2060,2070,2080,2090,2100,2110,2120,2130,2035,2045,2055

REM 10. Is player carrying anything at all?
2035 F1=-1:FORZ=0TOIL:IFIA(Z)=-1THEN z=Z:Z=IL:NEXTZ:Z=z:GOTO 2140 ELSE NEXTZ:F1=0:GOTO2140

REM 11. Is player carrying nothing?
2045 F1=0:FORZ=0TOIL:IFIA(Z)=-1THEN z=Z:Z=IL:NEXTZ:Z=z:GOTO 2140 ELSE NEXTZ:F1=-1:GOTO2140

REM 1. Is player carrying object LL?
2050 F1=IA(LL)=-1:GOTO2140

REM 12. Is obj LL not being carried by player and not in current room?
2055 F1=IA(LL)<>-1 AND IA(LL)<>R:GOTO2140

REM 2. Is player in room with object LL?
2060 F1=IA(LL)=R:GOTO2140

REM 3. Is player in room with obj LL or carrying it?
2070 F1=IA(LL)=R OR IA(LL)=-1:GOTO2140

REM 4. Is player in room LL?
2080 F1=R=LL:GOTO2140

REM 5. Is player carrying obj LL, or obj LL isn't in current room?
2090 F1=IA(LL)<>R:GOTO2140

REM 6. Is player not carrying obj LL?
2100 F1=IA(LL)<>-1:GOTO2140

REM 7. Is player not in room LL?
2110 F1=R<>LL:GOTO2140

REM 8. Is numbered flag-bit set?
2120 F1=SF AND INT(2^LL+.5):F1=F1<>0:GOTO2140

REM 9. Is numbered flag-bit clear?
2130 F1=SF AND INT(2^LL+.5):F1=F1=0:GOTO2140

2140 F2=F2 AND F1:IF F2 THEN NEXTY ELSE y=Y:Y=5:NEXTY:Y=y:NEXTX:GOTO2901

REM Process the commands
2150 IP=0:FORY=1TO4:K=INT((Y-1)/2)+6:ON Y GOTO 2160,2170,2160,2170
2160 AC=INT(CA(X,K)/150):GOTO2200
2170 AC=CA(X,K)-INT(CA(X,K)/150)*150
2200 IF AC>101 THEN 2205 ELSE IFAC=0 THEN 2600 ELSE IF AC<52 THEN PROCp(MS$(AC)):GOTO2600:ELSE ON AC-51 GOTO 2230,2260,2270,2280,2290,2300,2310,2280,2320,2340,2350,2360,2370,2380,2400,2420,2430,2440,2450,2261
2205 IF AC<102 THEN PRINT"BAD ACTION" ELSE PROCp(MS$(AC-50))
2207 GOTO2600

2210 L=DF:IF L THEN L=DF AND IA(9)<>R AND IA(9)<>-1:IF L PRINT"DANGEROUS TO MOVE IN THE DARK!"
2215 IFNV(1)<1PRINT"PLEASE GIVE ME A DIRECTION ALSO.":GOTO2930
2220 K=RM(R,NV(1)-1):IF K>=1 ELSE IF L THEN PRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=0:ELSE PRINT"I CAN'T GO IN THAT DIRECTION":GOTO2930
2222 IF NOT L PROCcls
2225 R=K:GOSUB1500:GOTO2930

REM 52. GETx
2230 L=0:FORZ=1TOIL:IFIA(Z)=-1LETL=L+1
2235 NEXTZ
2240 IF L>=MX PRINT"I'VE TOO MUCH TO CARRY":GOTO2600
2250 GOSUB4000:IA(P)=-1:GOTO2600

REM 53. DROPx
2260 GOSUB4000:IA(P)=R:GOTO2600

REM 71. SAVE
2261 PRINT"SAVING GAME":INPUT"READY OUTPUT TAPE "K$:PRINT;INT(IL*5/60)+1;" MINUTES"
2265 d=OPENOUT("SAV"):PRINT#d,SF,LX,DF,R:FORX1=0TOIL:PRINT#d,IA(X1):NEXTX1:CLOSE#d
2266 GOTO2600

REM 54. GOTOy
2270 GOSUB4000:R=P:GOTO2600

REM 55/59. x->RM0
2280 GOSUB4000:IA(P)=0:GOTO2600

REM 56. NIGHT
2290 DF=-1:GOTO2600

REM 57. DAY
2300 DF=0:GOTO2600

REM 58. SETz
2310 GOSUB4000
2315 SF=SF OR INT(.5+2^P):GOTO2600

REM 60. CLRz
2320 GOSUB4000

2325 SF=SF AND NOT INT(.5+2^P):GOTO2600

REM 61. DEAD
2340 PRINT"I'M DEAD...":R=RL:DF=0:GOTO2370

REM 62. x->y (Move the Par #1 object to the Par #2 room)
2350 GOSUB4000:L=P:GOSUB4000:IA(L)=P:GOTO2600

REM 63. FINI
2360 INPUT"THE GAME IS NOW OVER"'"ANOTHER GAME? "TP$:IF LEFT$(TP$,1)="N"THEN END ELSE FORX=0TOIL:IA(X)=I2(X):NEXTX:GOTO192

REM 64. DspRM
2370 GOSUB1500:GOTO2600

REM 65. SCORE
2380 L=0:FORZ=1TOIL:IF IA(Z)=TR IF LEFT$(IA$(Z),1)="*"LET L=L+1
2390 NEXTZ:PRINT"I'VE STORED ";L;" TREASURES."'"ON A SCALE OF 0 TO 100 THAT RATES A ";INT(L/TT*100):IF L=TT PRINT"WELL DONE.":GOTO2360
2395 GOTO2600

REM 66. INV
REM 2400 PRINT"I'M CARRYING THE FOLLOWING:":K=-1:FOR Z=0TOIL:IF IA(Z)<>-1 THEN 2410 ELSE GOSUB1525:IFLEN(TP$)+POS>63PRINT
2400 PRINT"I'M CARRYING THE FOLLOWING:":K=-1:FOR Z=0TOIL:IF IA(Z)<>-1 THEN 2410 ELSE GOSUB1525
2405 PROCp(CHR$(32*-(POS>0))+TP$+"."):PROCj:K=0
2410 NEXTZ:IF K PRINT"NOTHING"ELSE PRINT
2415 GOTO2600

REM 67. Redirect to 58.SETx (above) ****** In v4.6 terp this is SET0
2420 P=0:GOTO2310

REM 68. Redirect to 60.CLRz (above) ****** In v4.6 terp this is CLR0
2430 P=0:GOTO2320

REM 69. FILL (Re-fill the artificial light source (obj 9) and pick it up)
2440 LX=LT:IA(9)=-1:GOTO2600

REM 70. CLS
2450 PROCcls:GOTO2600

REM Next command
2600 NEXTY

REM Stop processing non-automatic actions
2610 IF NV(0)<>0 THEN x=X:X=CL:NEXTX:X=x:GOTO 2901

REM Next automatic action
2900 NEXTX
2901 :
2905 IF NV(0)=0 THEN2930
2907 GOSUB4200
2908 IF F PRINT"I DON'T UNDERSTAND YOUR COMMAND":GOTO2930
2910 IF NOT F2 PRINT"I CAN'T DO THAT YET":GOTO2930

REM Return from action-matching routine
2930 RETURN

REM Get param from condition list
4000 IP=IP+1:P=INT(CA(X,IP)/20):M=CA(X,IP)-P*20:IF M<>0 THEN 4000 ELSE RETURN

REM Automatically GET or DROP
4200 IF NV(0)<>10 AND NV(0)<>18 OR F3 THEN4290
4205 IF NV(1)=0 PRINT"WHAT?":GOTO4280
4206 IF NV(0)<>10 THEN 4210
4207 L=0:FORZ=0TOIL:IFIA(Z)=-1THENL=L+1
4208 NEXT:IF L>=MX PRINT"I'VE TOO MUCH TO CARRY":GOTO4280
4210 K=0:FOR X=0TOIL:IF RIGHT$(IA$(X),1)<>"/"THEN 4285 ELSE LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FOR Y=LL TO 2 STEP-1:IF MID$(TP$,Y,1)<>"/"THEN NEXTY:GOTO4285
4220 TP$=LEFT$(MID$(TP$,Y+1),ln)
4230 IFTP$<>NV$(NV(1),1)THEN4285
4240 IFNV(0)=10THEN4270
4250 IFIA(X)<>-1THENK=1:GOTO4285 ELSE IA(X)=R:K=3:GOTO4275
4270 IFIA(X)<>R THEN K=2:GOTO4285 ELSE IA(X)=-1:K=3
4275 PRINT"OK, ";:x=X:X=IL:NEXTX:X=x
4280 F=0:GOTO4290
4285 NEXTX
4286 IF K=1 THEN PRINT"I'M NOT CARRYING IT"ELSE IF K=2 PRINT"I DON'T SEE IT HERE"
4287 IF K=0 IF NOT F3 PRINT"ITS BEYOND MY POWER TO DO THAT":F=0
4288 IF K<>0 THEN F=0
4290 RETURN

5000 INPUT"REWIND/LOAD ADVENTURE DATA TAPE."'"HIT ENTER "TP$
5005 d=OPENIN("DAT")
5010 INPUT#d,IL,CL,NL,RL,MX,AR,TT,ln,LT,ML,TR
REM 5012 LPRINT IL;",";CL;",";NL;",";RL;",";MX;",";AR;",";TT;",";ln;",";LT;",";ML;",";TR
REM 5015 A=(IL+CL/2+NL/10+RL+ML)/12:PRINT:PRINT"TAPE WILL TAKE";A+1;"MINUTES TO LOAD."
5020 DIMNV(1),CA(CL,7),NV$(NL,1),IA$(IL),IA(IL),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
5030 FORX=0TOCL STEP2:Y=X+1:INPUT#d,CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7)
REM 5032 LPRINT CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7)
5034 NEXT
5040 FORX=0TONL STEP10:FORY=0TO1:INPUT#d,NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y)
REM 5042 LPRINT NV$(X,Y):LPRINT NV$(X+1,Y):LPRINT NV$(X+2,Y):LPRINT NV$(X+3,Y):LPRINT NV$(X+4,Y):LPRINT NV$(X+5,Y):LPRINT NV$(X+6,Y):LPRINT NV$(X+7,Y):LPRINT NV$(X+8,Y):LPRINT NV$(X+9,Y)
5045 NEXTY,X
5060 FORX=0TORL:INPUT#d,RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X)
REM 5063 LPRINT RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X)
5065 NEXT
5070 FORX=0TOML:INPUT#d,MS$(X)
REM 5073 LPRINT MS$(X)
5075 NEXT
5080 FORX=0TOIL:INPUT#d,IA$(X),IA(X):I2(X)=IA(X)
REM LPRINT IA$(X):LPRINT IA(X):
5082 NEXT:CLOSE#d:RETURN

6000 DEFPROCcls:CLS:PRINT:ENDPROC

REM Word-wrap
6060 DEFPROCp($S%):LOCALA%,Z%,C%,N%,T%:N%=LEN$S%:A%=0:Z%=L%+1-POS:REPEATIFZ%>N%Z%=N%ELSEREPEATZ%=Z%-1:C%=S%?Z%:UNTILC%=32:IFZ%<A%Z%=A%+L%
6070 T%=S%?Z%:S%?Z%=13:PRINT$(S%+A%);:S%?Z%=T%:VDU32,-8*(POS=1):IFZ%-A%<=L%ANDPOS PRINTELSEIFC%=32A%=A%+1ELSEZ%=Z%-1
6080 A%=Z%+1:Z%=A%+L%+1:UNTILA%>=N%:IFPOS PRINT:ENDPROC ELSEENDPROC

REM Backtrack to previous non-space char 
6090 DEFPROCj:LOCALA%,C%:A%=&87:REPEATVDU8:C%=(USR(&FFF4)AND&FF00)DIV256:UNTILC%<>32:VDU9:ENDPROC

REM Is the previous line blank?
6100 DEFFNb:LOCALA%,C%,I%:VDU11,8:A%=&87:FORI%=1TOL%:VDU9:C%=(USR(&FFF4)AND&FF00)DIV256:IFC%=32NEXT:VDU10,13:=TRUE ELSEI%=L%:NEXT:VDU10,13:=FALSE
REM If at start of line, then print a blank line if prev line isn't blank.
6120 DEFPROCb:IFPOS ENDPROC ELSE IFNOT(FNb)PRINT:ENDPROC ELSEENDPROC
