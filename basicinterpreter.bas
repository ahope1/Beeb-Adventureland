1 'COPYRIGHT SCOTT ADAMS. 1978. 1-305-862-6917.
5 CLEAR5400:DEFINTA-Z:IFPEEK(16396)<>201THEN CMD"T"
78 X=Y=Z:K=R=V:N=LL=F:TP$=K$:GOTO190
80 CLS:PRINT"     ***   WELCOME TO ADVENTURE LAND. (#4.2) ***":PRINT:PRINT" UNLESS TOLD DIFFERENTLY YOU MUST FIND *TREASURES*
AND-RETURN-THEM-TO-THEIR-PROPER-PLACE!"
85 PRINT:PRINT"I'M YOUR PUPPET. GIVE ME ENGLISH COMMANDS THAT"
90 PRINT"CONSIST OF A NOUN AND VERB. SOME EXAMPLES...":PRINT:PRINT"TO FIND OUT WHAT YOU'RE CARRYING YOU MIGHT SAY: TAKE INVENTORY
TO GO INTO A HOLE YOU MIGHT SAY: GO HOLE
TO SAVE CURRENT GAME: SAVE GAME"
100 PRINT:PRINT"YOU WILL AT TIMES NEED SPECIAL ITEMS TO DO THINGS, BUT I'M
SURE YOU'LL BE A GOOD ADVENTURER AND FIGURE THESE THINGS OUT."
120 PRINT:INPUT"     HAPPY ADVENTURING... HIT ENTER TO START";K$:CLS:RETURN
190 GOSUB5000
192 R=AR:LX=LT:DF=0:SF=0:INPUT"RETRIEVE OLD 'SAVED' GAME";K$:IFLEFT$(K$,1)<>"Y"THEN200
193 INPUT"READY SAVED TAPE";K$:PRINTINT(IL*5/60)+1;"MINUTES"
195 INPUT#-1,SF,LX,DF,R:FORX=0TOIL:INPUT#-1,IA(X):NEXT
200 GOSUB80:CLS:GOSUB1500:GOTO225
210 INPUT"TELL ME WHAT TO DO";TP$:PRINT:GOSUB1000:IFFPRINT"YOU USE WORD(S) I DON'T KNOW":GOTO210
215 IFIL>8IFIA(9)=-1THENLX=LX-1:IFLX<0THENPRINT"LIGHT HAS RUN OUT":IA(9)=0ELSEIFLX<25PRINT"LIGHT RUNS OUT IN";LX;"TURNS!"
220 GOSUB2000
225 NV(0)=0:GOSUB2000:GOTO210
1000 K=0:NT$(0)="":NT$(1)=""
1010 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1ELSENT$(K)=NT$(K)+K$
1030 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN1070ELSENT$(X)=LEFT$(NT$(X),LN):FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
1035 IFX=1IFY<7THENK$=LEFT$(K$,LN)
1040 IFNT$(X)=K$THENNV(X)=YELSENEXTY:GOTO1070
1060 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO1060
1070 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN
1500 IFDFIFIA(9)<>-1ANDIA(9)<>RPRINT"I CAN'T SEE, ITS TOO DARK.":RETURN
1505 K=-1:IFLEFT$(RS$(R),1)="*"PRINTMID$(RS$(R),2);:GOTO1520
1510 PRINT"I'M IN A ";RS$(R);
1520 FORZ=0TOIL:IFKIFIA(Z)=RPRINT". VISIBLE ITEMS HERE ARE:
":K=0
1524 GOTO1530
1525 TP$=IA$(Z):IFRIGHT$(TP$,1)="/"FORW=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1)ELSENEXTW
1526 RETURN
1530 IFIA(Z)<>RTHEN1540ELSEGOSUB1525:IFPOS(0)+LEN(TP$)+3>63THENPRINT
1535 PRINTTP$;".  ";
1540 NEXT:PRINT
1550 K=-1:FORZ=0TO5:IFKIFRM(R,Z)<>0PRINT"
OBVIOUS EXITS: ";:K=0
1560 IFRM(R,Z)<>0PRINTNV$(Z+1,1);" ";;
1570 NEXT:IFNOTKPRINT
1580 PRINT:RETURN
2000 F2=-1:F=-1:F3=0:IFNV(0)=1ANDNV(1)<7THEN2210ELSEFORX=0TOCL:V=CA(X,0)/150:IFNV(0)=0IFV<>0THEN2930
2005 IFNV(0)<>VTHENNEXTX:GOTO2901ELSEN=CA(X,0)-V*150
2010 IFNV(0)=0THENF=0:IFRND(100)<=NTHEN2030ELSENEXTX:GOTO2901
2020 IFN<>NV(1)ANDN<>0THENNEXTX:GOTO2901
2030 F2=-1:F=0:F3=-1:FORY=1TO5:LL=CA(X,Y)/20:K=CA(X,Y)-LL*20:F1=-1:ONK+1GOTO2140,2050,2060,2070,2080,2090,2100,2110,2120,2130,2035,2045,2055
2035 F1=-1:FORZ=0TOIL:IFIA(Z)=-1THEN2140ELSENEXT:F1=0:GOTO2140
2045 F1=0:FORZ=0TOIL:IFIA(Z)=-1THEN2140ELSENEXT:F1=-1:GOTO2140
2050 F1=IA(LL)=-1:GOTO2140
2055 F1=IA(LL)<>-1ANDIA(LL)<>R:GOTO2140
2060 F1=IA(LL)=R:GOTO2140
2070 F1=IA(LL)=RORIA(LL)=-1:GOTO2140
2080 F1=R=LL:GOTO2140
2090 F1=IA(LL)<>R:GOTO2140
2100 F1=IA(LL)<>-1:GOTO2140
2110 F1=R<>LL:GOTO2140
2120 F1=SFANDCINT(2[LL+.5):F1=F1<>0:GOTO2140
2130 F1=SFANDCINT(2[LL+.5):F1=F1=0:GOTO2140
2140 F2=F2ANDF1:IFF2THENNEXTYELSENEXTX:GOTO2901
2150 IP=0:FORY=1TO4:K=(Y-1)/2+6:ONYGOTO2160,2170,2160,2170
2160 AC=CA(X,K)/150:GOTO2200
2170 AC=CA(X,K)-CINT(CA(X,K)/150)*150
2200 IFAC>101THEN2205ELSEIFAC=0THEN2600ELSEIFAC<52THENPRINTMS$(AC):GOTO2600:ELSEONAC-51GOTO2230,2260,2270,2280,2290,2300,2310,2280,2320,2340,2350,2360,2370,2380,2400,2420,2430,2440,2450,2261
2205 IFAC<102THENPRINT"BAD ACTION"ELSEPRINTMS$(AC-50)
2207 GOTO2600
2210 L=DF:IFLTHENL=DFANDIA(9)<>R ANDIA(9)<>-1:IFL PRINT"DANGEROUS TO MOVE IN THE DARK!"
2215 IFNV(1)<1PRINT"PLEASE GIVE ME A DIRECTION ALSO.":GOTO2930
2220 K=RM(R,NV(1)-1):IFK<1IFLTHENPRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=0:ELSEPRINT"I CAN'T GO IN THAT DIRECTION":GOTO2930
2222 IFNOTLCLS
2225 R=K:GOSUB1500:GOTO2930
2230 L=0:FORZ=1TOIL:IFIA(Z)=-1LETL=L+1
2235 NEXTZ
2240 IFL>=MXPRINT"I'VE TOO MUCH TO CARRY":GOTO2600
2250 GOSUB4000:IA(P)=-1:GOTO2600
2260 GOSUB4000:IA(P)=R:GOTO2600
2261 PRINT"SAVING GAME":INPUT"READY OUTPUT TAPE";K$:PRINTINT(IL*5/60)+1;"MINUTES"
2265 PRINT#-1,SF,LX,DF,R:FORX1=0TOIL:PRINT#-1,IA(X1):NEXTX1
2266 GOTO2600
2270 GOSUB4000:R=P:GOTO2600
2280 GOSUB4000:IA(P)=0:GOTO2600
2290 DF=-1:GOTO2600
2300 DF=0:GOTO2600
2310 GOSUB4000
2315 SF=SF ORCINT(.5+2[P):GOTO2600
2320 GOSUB4000
2325 SF=SFANDNOTCINT(.5+2[P):GOTO2600
2340 PRINT"I'M DEAD...":R=RL:DF=0:GOTO2370
2350 GOSUB4000:L=P:GOSUB4000:IA(L)=P:GOTO2600
2360 PRINT"THE GAME IS NOW OVER":INPUT"ANOTHER GAME";TP$:IFLEFT$(TP$,1)="N"THENENDELSEFORX=0TOIL:IA(X)=I2(X):NEXTX:GOTO192
2370 GOSUB1500:GOTO2600
2380 L=0:FORZ=1TOIL:IFIA(Z)=TRIFLEFT$(IA$(Z),1)="*"LETL=L+1
2390 NEXTZ:PRINT"I'VE STORED";L;"TREASURES.
ON A SCALE OF 0 TO 100 THAT RATES A";CINT(L/TT*100):IFL=TTPRINT"WELL DONE.":GOTO2360
2395 GOTO2600
2400 PRINT"I'M CARRYING THE FOLLOWING:":K=-1:FORZ=0TOIL:IFIA(Z)<>-1THEN2410ELSEGOSUB1525:IFLEN(TP$)+POS(0)>63PRINT
2405 PRINTTP$;".",;:K=0
2410 NEXTZ:IFKPRINT"NOTHING"ELSEPRINT
2415 GOTO2600
2420 P=0:GOTO2310
2430 P=0:GOTO2320
2440 LX=LT:IA(9)=-1:GOTO2600
2450 CLS:GOTO2600
2600 NEXTY
2610 IFNV(0)<>0THEN2901
2900 NEXTX
2901 '
2905 IFNV(0)=0THEN2930
2907 GOSUB4200
2908 IFFPRINT"I DON'T UNDERSTAND YOUR COMMAND":GOTO2930
2910 IFNOTF2PRINT"I CAN'T DO THAT YET":GOTO2930
2930 RETURN
4000 IP=IP+1:P=CA(X,IP)/20:M=CA(X,IP)-P*20:IFM<>0THEN4000ELSERETURN
4200 IFNV(0)<>10ANDNV(0)<>18ORF3THEN4290
4205 IFNV(1)=0PRINT"WHAT?":GOTO4280
4206 IFNV(0)<>10THEN4210
4207 L=0:FORZ=0TOIL:IFIA(Z)=-1THENL=L+1
4208 NEXT:IFL>=MXPRINT"I'VE TOO MUCH TO CARRY":GOTO4280
4210 K=0:FORX=0TOIL:IFRIGHT$(IA$(X),1)<>"/"THEN4285ELSELL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FORY=LLTO2STEP-1:IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO4285
4220 TP$=LEFT$(MID$(TP$,Y+1),LN)
4230 IFTP$<>NV$(NV(1),1)THEN4285
4240 IFNV(0)=10THEN4270
4250 IFIA(X)<>-1THENK=1:GOTO4285ELSEIA(X)=R:K=3:GOTO4275
4270 IFIA(X)<>RTHENK=2:GOTO4285ELSEIA(X)=-1:K=3
4275 PRINT"OK, ";
4280 F=0:GOTO4290
4285 NEXTX
4286 IFK=1THENPRINT"I'M NOT CARRYING IT"ELSEIFK=2PRINT"I DON'T SEE IT HERE"
4287 IFK=0IFNOTF3PRINT"ITS BEYOND MY POWER TO DO THAT":F=0
4288 IFK<>0THENF=0
4290 RETURN
5000 INPUT"REWIND/LOAD ADVENTURE DATA TAPE. HIT ENTER";TP$
5010 INPUT#-1,IL,CL,NL,RL,MX,AR,TT,LN,LT,ML,TR
5012 LPRINT IL;",";CL;",";NL;",";RL;",";MX;",";AR;",";TT;",";LN;",";LT;",";ML;",";TR
5015 A=(IL+CL/2+NL/10+RL+ML)/12:PRINT:PRINT"TAPE WILL TAKE";A+1;"MINUTES TO LOAD."
5020 DIMNV(1),CA(CL,7),NV$(NL,1),IA$(IL),IA(IL),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
5030 FORX=0TOCL STEP2:Y=X+1:INPUT#-1,CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7)
5032 LPRINT CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7)
5034 NEXT
5040 FORX=0TONLSTEP10:FORY=0TO1:INPUT#-1,NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y)
5042 LPRINT NV$(X,Y):LPRINT NV$(X+1,Y):LPRINT NV$(X+2,Y):LPRINT NV$(X+3,Y):LPRINT NV$(X+4,Y):LPRINT NV$(X+5,Y):LPRINT NV$(X+6,Y):LPRINT NV$(X+7,Y):LPRINT NV$(X+8,Y):LPRINT NV$(X+9,Y)
5045 NEXTY,X
5060 FORX=0TORL:INPUT#-1,RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X)
5063 LPRINT RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X)
5065 NEXT
5070 FORX=0TOML:INPUT#-1,MS$(X)
5073 LPRINT MS$(X)
5075 NEXT
5080 FORX=0TOIL:INPUT#-1,IA$(X),IA(X):I2(X)=IA(X)
5082 LPRINT IA$(X):LPRINT IA(X):NEXT:RETURN
